#pragma kernel CalcInput

#include "../cginc/GPUTrails.cginc"

float _Time;
float _UpdateDistanceMin;
uint _TrailNum;

RWStructuredBuffer<Trail> _TrailBuffer;
RWStructuredBuffer<Node> _NodeBuffer;

Node GetNode(int trailIdx, int nodeIdx)
{
    // ToNodeBufIdx はnodebufferから指定のnodeのindexを探索する
	return _NodeBuffer[ToNodeBufIdx(trailIdx, nodeIdx)];
}

void SetNode(Node node, int trailIdx, int nodeIdx)
{
	_NodeBuffer[ToNodeBufIdx(trailIdx, nodeIdx)] = node;
}

////////////////////////////////////////////////////////////////////////////////
// Input
////////////////////////////////////////////////////////////////////////////////
StructuredBuffer<Input> _InputBuffer;

[numthreads(256,1,1)]
void CalcInput (uint3 id : SV_DispatchThreadID)
{
    // trail以上のidで呼ばれることがあるため、if制御
	uint trailIdx = id.x;
	if ( trailIdx < _TrailNum)
	{	
		Trail trail = _TrailBuffer[trailIdx];
		Input input = _InputBuffer[trailIdx];
		int currentNodeIdx = trail.currentNodeIdx;

		bool update = true;
        // 負数の場合は未使用
		if ( trail.currentNodeIdx >= 0 )
		{
            // trail IdとNodeIndexからnodeを取得
			Node node = GetNode(trailIdx, currentNodeIdx);
            // input位置と最新のnodeの位置の距離
			float dist = distance(input.position, node.position);
            // 最小距離より大きい場合に更新する
			update = dist > _UpdateDistanceMin;
		}

		if ( update )
		{
            // nodeの更新
			Node node;
			node.time = _Time;
			node.position = input.position;

            // currentNodeIdxの更新
			currentNodeIdx++;
			currentNodeIdx %= _NodeNumPerTrail; // _NodeNumPerTrail は life * MAX_FPS
            // trailあたりのnode数を超えたらリングバッファ状になるように

			// write new node
			SetNode(node, trailIdx, currentNodeIdx);

			// update trail
			trail.currentNodeIdx = currentNodeIdx;
			_TrailBuffer[trailIdx] = trail;
		}
	}
}
